#include "PostEffectUtil.hlsli"

ByteAddressBuffer Histogram : register(t0);
RWStructuredBuffer<float> Exposure : register(u0);

cbuffer cb0 : register(b1)
{
    float TargetLuminance;
    float AdaptationRate;
    float MinExposure;
    float MaxExposure;
    uint PixelCount;
}

groupshared float gAccum[256];

[numthreads(256, 1, 1)]
void main(uint GroupIndex : SV_GroupIndex)
{
    float WeightedSum = (float) GroupIndex * (float) Histogram.Load(GroupIndex * 4);

    [unroll]
    for (uint i = 1; i < 256; i *= 2)
    {
        gAccum[GroupIndex] = WeightedSum;               // Write
        GroupMemoryBarrierWithGroupSync();              // Sync
        WeightedSum += gAccum[(GroupIndex + i) % 256];  // Read
        GroupMemoryBarrierWithGroupSync();              // Sync
    }

    //画像全体が\の龊悉下冻訾蛘{整しないでください
    if (WeightedSum == 0.0)
        return;

    float MinLog = Exposure[4];
    float MaxLog = Exposure[5];
    float LogRange = Exposure[6];
    float RcpLogRange = Exposure[7];

    // 平均ヒストグラムは、すべてのピクセルの加重合をピクセルt数で割った
    // 加重を与えなかったピクセル（つまり\ピクセル）を除いた
    float weightedHistAvg = WeightedSum / (max(1, PixelCount - Histogram.Load(0))) - 1.0;
    float logAvgLuminance = exp2(weightedHistAvg / 254.0 * LogRange + MinLog);
    float targetExposure = TargetLuminance / logAvgLuminance;
    //float targetExposure = -log2(1 - TargetLuminance) / logAvgLuminance;

    float exposure = Exposure[0];
    exposure = lerp(exposure, targetExposure, AdaptationRate);
    exposure = clamp(exposure, MinExposure, MaxExposure);

    if (GroupIndex == 0)
    {
        Exposure[0] = exposure;
        Exposure[1] = 1.0 / exposure;
        Exposure[2] = exposure;
        Exposure[3] = weightedHistAvg;

        //ヒストグラムを数平均を中心に再中心化する最初のみ
        float biasToCenter = (floor(weightedHistAvg) - 128.0) / 255.0;
        if (abs(biasToCenter) > 0.1)
        {
            MinLog += biasToCenter * RcpLogRange;
            MaxLog += biasToCenter * RcpLogRange;
        }

        //TODO: の欷撕悉Δ瑜Δ恕数欷pします。
        //(アイデア) ^小または^大に代表されるO端な境界については、
        //中gの数加重合を_Jします。
        //つまり、for ル`プを2つに分割して、16 のグル`プの合を算し、
        //I端のグル`プをチェックしてから、再⒑嫌を完了します。
        Exposure[4] = MinLog;
        Exposure[5] = MaxLog;
        Exposure[6] = LogRange;
        Exposure[7] = 1.0 / LogRange;
    }
}