#include "PostEffectUtil.hlsli"

SamplerState BiLinearClamp : register(s0);
Texture2D<float3> SourceTex : register(t0);
StructuredBuffer<float> Exposure : register(t1);
RWTexture2D<uint> LumaResult : register(u0);

cbuffer cb0
{
    float2 ginverseOutputSize;
}

[numthreads(8, 8, 1)]
void main(uint3 DTid : SV_DispatchThreadID)
{
    //4つのサンプルがカバ`する象限のちょうど中央にくるように、スケ`ルS数と1ピクセルのサイズが必要です。
    float2 uv = DTid.xy * ginverseOutputSize;
    float2 offset = ginverseOutputSize * 0.25f;

    //2倍以上s小したときにアンダ`サンプリングがk生しないように、4つのバイリニアサンプルを使用します。
    float3 color1 = SourceTex.SampleLevel(BiLinearClamp, uv + float2(-offset.x, -offset.y), 0);
    float3 color2 = SourceTex.SampleLevel(BiLinearClamp, uv + float2(offset.x, -offset.y), 0);
    float3 color3 = SourceTex.SampleLevel(BiLinearClamp, uv + float2(-offset.x, offset.y), 0);
    float3 color4 = SourceTex.SampleLevel(BiLinearClamp, uv + float2(offset.x, offset.y), 0);

    //平均x度を算する
    float luma = RGBToLuminance(color1 + color2 + color3 + color4) * 0.25;

    //log(0)を防止し、Histogram[0]にな\ピクセルのみを配置します
    if (luma == 0.0)
    {
        LumaResult[DTid.xy] = 0;
    }
    else
    {
        const float MinLog = Exposure[4];
        const float RcpLogRange = Exposure[7];
        float logLuma = saturate((log2(luma) - MinLog) * RcpLogRange); //[0.0, 1.0]
        LumaResult[DTid.xy] = logLuma * 254.0 + 1.0; //[1, 255]
    }
}